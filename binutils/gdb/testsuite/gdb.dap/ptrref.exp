# Copyright 2023-2024 Free Software Foundation, Inc.

# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

require allow_dap_tests

load_lib dap-support.exp

standard_testfile .cc

if {[build_executable ${testfile}.exp $testfile $srcfile {debug c++}] == -1} {
    return
}

if {[dap_launch $testfile] == ""} {
    return
}

set line [gdb_get_line_number "BREAK"]
set obj [dap_check_request_and_response "set breakpoint by line number" \
	     setBreakpoints \
	     [format {o source [o path [%s]] breakpoints [a [o line [i %d]]]} \
		  [list s $srcfile] $line]]
set line_bpno [dap_get_breakpoint_number $obj]

dap_check_request_and_response "start inferior" configurationDone
dap_wait_for_event_and_check "inferior started" thread "body reason" started

dap_wait_for_event_and_check "stopped at line breakpoint" stopped \
    "body reason" breakpoint \
    "body hitBreakpointIds" $line_bpno

set bt [lindex [dap_check_request_and_response "backtrace" stackTrace \
		    {o threadId [i 1]}] \
	    0]
set frame_id [dict get [lindex [dict get $bt body stackFrames] 0] id]

set scopes [dap_check_request_and_response "get scopes" scopes \
		[format {o frameId [i %d]} $frame_id]]
set scopes [dict get [lindex $scopes 0] body scopes]

gdb_assert {[llength $scopes] == 2} "two scopes"

lassign $scopes scope reg_scope
gdb_assert {[dict get $scope name] == "Locals"} "scope is locals"

gdb_assert {[dict get $scope namedVariables] == 4} "three vars in scope"

set num [dict get $scope variablesReference]
set refs [lindex [dap_check_request_and_response "fetch variables" \
		      "variables" \
		      [format {o variablesReference [i %d] count [i 3]} \
			   $num]] \
	      0]

proc fetch_pointer {name var} {
    set num [dict get $var variablesReference]
    set refs [lindex [dap_check_request_and_response "fetch children of $name" \
			  "variables" \
			  [format {o variablesReference [i %d] count [i 1]} \
			       $num]] \
		  0]
    set var [lindex [dict get $refs body variables] 0]
    gdb_assert {[dict get $var value] == 23} \
	"value of child of $name"
    gdb_assert {[dict get $var variablesReference] == 0} \
	"no children for child of $name"
}

foreach var [dict get $refs body variables] {
    set name [dict get $var name]

    switch $name {
	"value" {
	    gdb_assert {[dict get $var value] == "23"} "check value of value"
	    gdb_assert {[dict get $var variablesReference] == 0} \
		"$name does not have child"
	}
	"ptr" -
	"ref" {
	    gdb_assert {[dict get $var variablesReference] != 0} \
		"$name has children"
	    # The implementation somewhat arbitrarily chooses "named"
	    # children here.
	    gdb_assert {[dict get $var namedVariables] == 1} \
		"$name has exactly one child"
	    fetch_pointer $name $var
	}
	"aggregate" {
	    gdb_assert {[dict get $var variablesReference] != 0} \
		"$name has children"
	    # This should omit the static field.
	    gdb_assert {[dict get $var namedVariables] == 2} \
		"$name has exactly 2 children"
	}
    }
}

dap_shutdown
